! ===================================================================================================\n
! MODULE        : ioipsl_ioget_mon_len_julian
!
! CONTACT       : orchidee-help _at_ listes.ipsl.fr
!
! LICENCE       : IPSL (2006)
! This software is governed by the CeCILL licence see ORCHIDEE/ORCHIDEE_CeCILL.LIC
!
!>\BRIEF        Unit tests for IOIPSL ioget_mon_len in julian calendar 
!!
!!\n DESCRIPTION : 
!!
!!
!! RECENT CHANGE(S) : None
!!
!! REFERENCE(S) :
!!
!! SVN          :
!! $HeadURL: svn://forge.ipsl.jussieu.fr/orchidee/branches/ORCHIDEE-MICT/ORCHIDEE/src_sechiba/hydrol.f90 $
!! $Date: 2018-10-09 11:43:16 +0200 (Tue, 09 Oct 2018) $
!! $Revision: 5477 $
!! \n
!_ ===============================================================================================\n

module ioipsl_ioget_mon_len_julian_mod

  use ioipsl
  use pfunit_mod
  implicit none

  public :: testcase_ioipsl_julian

  @testParameter
  type, extends(AbstractTestParameter) :: TestDateParam 
      integer :: year
      integer :: month
      integer :: totalDays 
  contains
      procedure :: toString
  end type TestDateParam 

  @TestCase(testParameters={getParameters()}, constructor=newTest)
  type, extends(ParameterizedTestCase) :: testcase_ioipsl_julian
      integer :: year
      integer :: month
      integer :: totalDays 

    contains
      procedure :: setUp     ! overides generic
      procedure :: tearDown  ! overrides generic
  end type testcase_ioipsl_julian

contains

! Constructor
   function newTest(testParameter) result(tst)
      type (testcase_ioipsl_julian) :: tst
      type (TestDateParam), intent(in) :: testParameter

      integer :: year 
      integer :: month
      integer :: totalDays

      tst%year = testParameter%year
      tst%month = testParameter%month
      tst%totalDays = testParameter%totalDays

   end function newTest  

!-------------------
   function getParameters() result(params)
      type (TestDateParam), allocatable :: params(:)

!                             year   month    result
      params = [ &
           & TestDateParam(   1988,    2,   29), &
           & TestDateParam(   1989,    2,   28), &
           & TestDateParam(   1892,    2,   29), &
           & TestDateParam(   1900,    2,   29), &
           & TestDateParam(   1904,    2,   29) &
           & ]

   end function getParameters



@Test
   subroutine test_ioipsl_ioget_mon_len_julian(this)
      class (Testcase_ioipsl_julian), intent(inout) :: this

      INTEGER :: daysFound

      daysFound = ioget_mon_len(this%year, this%month)

      @assertEqual(this%totalDays, daysFound, message='Calculated days do not match;')

   end subroutine test_ioipsl_ioget_mon_len_julian


   function toString(this) result(string)
      class (TestDateParam), intent(in) :: this
      character(:), allocatable :: string

      character(len=500) :: buffer

      write(buffer,'("(year,month,result)=(",i4,",",i2,",",i4,")")') &
           & this%year, this%month, this%totalDays
      string = trim(buffer)

   end function toString
  
!! ================================================================================================================================
!! SUBROUTINE 	: test_ioipsl_julian_setUp
!!
!>\BRIEF         Initialize IOISPL calendar (julian)
!!
!! DESCRIPTION : 
!!
!! MAIN OUTPUT VARIABLE(S) : 
!!
!! REFERENCE(S) : 
!!
!! FLOWCHART    : None
!! \n
!_ ================================================================================================================================
  SUBROUTINE setUp(this)
    class (testcase_ioipsl_julian), intent(inout) :: this

    CALL ioconf_calendar('julian')

  END SUBROUTINE setUp
  
!! ================================================================================================================================
!! SUBROUTINE 	: test_ioipsl_julian_tearDown
!!
!>\BRIEF         release IOISPL calendar
!!
!! DESCRIPTION : 
!!
!! MAIN OUTPUT VARIABLE(S) : 
!!
!! REFERENCE(S) : 
!!
!! FLOWCHART    : None
!! \n
!_ ================================================================================================================================
  SUBROUTINE tearDown(this)
    class (testcase_ioipsl_julian), intent(inout) :: this
    LOGICAL :: old_stats

    CALL lock_calendar (.FALSE., old_stats)
  END SUBROUTINE tearDown
  
end module ioipsl_ioget_mon_len_julian_mod
